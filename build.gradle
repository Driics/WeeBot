buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        // maven { url 'https://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:3.3.2")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.20"
        // Import Spring dependency-management plugin for classic "apply plugin" usage in subprojects
        classpath "io.spring.gradle:dependency-management-plugin:1.1.6"
    }
}

group = 'ru.driics'
version = '1.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    // Centralize dependency versions via BOMs so module build.gradle files can omit versions
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.3.2"
            // Align Spring Cloud with the selected Spring Boot version
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.3"
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1"

        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }
}